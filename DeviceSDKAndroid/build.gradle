apply plugin: 'com.android.library'

android {
	compileSdkVersion 33
	buildToolsVersion "30.0.3"

	defaultConfig {
		minSdkVersion 23
		targetSdkVersion 33
		versionCode 1
		versionName project.version

		testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

		javaCompileOptions {
			annotationProcessorOptions {
				arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
			}
		}
	}

	buildTypes {
		release {
			minifyEnabled false
		}
	}

	packagingOptions {
		exclude 'META-INF/DEPENDENCIES'
		exclude 'META-INF/LICENSE'
		exclude 'META-INF/LICENSE.txt'
		exclude 'META-INF/license.txt'
		exclude 'META-INF/NOTICE'
		exclude 'META-INF/NOTICE.txt'
		exclude 'META-INF/notice.txt'
		exclude 'META-INF/ASL2.0'
	}
	lintOptions {
		disable 'InvalidPackage'
	}

	testOptions {
		unitTests.includeAndroidResources = true
	}
}

repositories {
	maven {
		url "https://repo.eclipse.org/content/repositories/paho-releases/"
	}
}

dependencies {
	api project(':DeviceSDK')
	implementation 'org.eclipse.paho:org.eclipse.paho.android.service:1.1.1'

	def room_version = "2.3.0"
	implementation "androidx.room:room-runtime:$room_version"
	annotationProcessor "androidx.room:room-compiler:$room_version"
	androidTestImplementation "androidx.room:room-testing:$room_version"

	testImplementation 'junit:junit:4.13'
	androidTestImplementation 'androidx.test:runner:1.4.0'
	androidTestImplementation 'org.mongodb:bson:3.11.0'
	androidTestImplementation 'com.squareup.okhttp3:okhttp:3.12.1'
	// Optional -- Mockito framework
	testImplementation 'org.mockito:mockito-core:3.12.4'
	androidTestImplementation 'com.linkedin.dexmaker:dexmaker-mockito:2.28.1'
}

task javadoc(type: Javadoc) {
	failOnError false
	group = "Documentation"
	description = "Generate Javadoc"
	source = android.sourceSets.main.java.srcDirs
	classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
	options.memberLevel = JavadocMemberLevel.PROTECTED
	options.addStringOption('Xmaxwarns', '1')
	options.addStringOption('Xmaxerrs', '1')
	options.links "https://docs.oracle.com/javase/8/docs/api"
	options.linksOffline "https://developer.android.com/reference",
			"${android.sdkDirectory}/docs/reference"
	android.libraryVariants.all { variant ->
		if (variant.name == 'release') {
			owner.classpath += variant.javaCompileProvider.get().classpath
		}
	}
	exclude '**/R.html', '**/R.*.html', '**/index.html'
}

ext {
	PUBLISH_GROUP_ID = project.group
	PUBLISH_VERSION = project.version
	PUBLISH_ARTIFACT_ID = 'devicesdk-android'
}

apply from: "${rootProject.projectDir}/scripts/publish-module.gradle"
